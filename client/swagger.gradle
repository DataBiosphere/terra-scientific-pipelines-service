dependencies {
    api 'org.springframework.boot:spring-boot-starter-json'

    // Version controlled by dependency management plugin
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    implementation 'org.glassfish.jersey.core:jersey-client'
    implementation 'org.glassfish.jersey.media:jersey-media-json-jackson'
    implementation 'org.glassfish.jersey.media:jersey-media-multipart'
    implementation 'org.glassfish.jersey.connectors:jersey-jdk-connector:3.1.2'

    implementation 'com.brsanthu:migbase64:2.2' // for Base64 encoding

    implementation 'io.swagger.core.v3:swagger-annotations'
    swaggerCodegen 'io.swagger.codegen.v3:swagger-codegen-cli'

    implementation platform('com.google.cloud:libraries-bom:26.28.0') // required for TCL
}

def artifactGroup = "${group}.pipelines"

generateSwaggerCode {
    inputFile = file('../common/openapi.yml')
    language = 'java'
    library = 'jersey2'

    components = [
        apiDocs  : false, apiTests: false,
        modelDocs: false, modelTests: false
    ]

    additionalProperties = [
        modelPackage  : "${artifactGroup}.model",
        apiPackage    : "${artifactGroup}.api",
        invokerPackage: "${artifactGroup}.client",
        dateLibrary   : 'java11',
        jakarta       : true
    ]

    rawOptions = ['--ignore-file-override', "${projectDir}/.swagger-codegen-ignore"]
}

idea.module.generatedSourceDirs = [file("${generateSwaggerCode.outputDir}/src/main/java")]
sourceSets.main.java.srcDir "${generateSwaggerCode.outputDir}/src/main/java"
compileJava.dependsOn generateSwaggerCode
sourcesJar.dependsOn generateSwaggerCode
