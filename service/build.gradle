
import org.springframework.boot.gradle.plugin.SpringBootPlugin

buildscript {
    dependencies {
        classpath("org.liquibase:liquibase-core:5.0.1")
    }
}

plugins {
    id 'bio.terra.pipelines.java-spring-app-conventions'
    id 'de.undercouch.download'
    id 'com.google.cloud.tools.jib'
    id 'com.srcclr.gradle'
    id 'org.sonarqube'
    id 'com.jfrog.artifactory'
    id 'org.liquibase.gradle' version '3.1.0'
    id 'org.hidetake.swagger.generator'
    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
    id 'com.gorylenko.gradle-git-properties' version '2.4.1'
    id 'jacoco'
}

apply from: 'publishing.gradle'
apply from: 'generators.gradle'

dependencyManagement {
    imports {
        mavenBom(SpringBootPlugin.BOM_COORDINATES)
    }
}

configurations {
    // https://stackoverflow.com/questions/76706612/standard-commons-logging-discovery-in-action-with-spring-jcl
    // resolves "Standard Commons Logging discovery in action with spring-jcl: please remove commons-logging.jar from classpath in order to avoid potential conflicts"
    configureEach {
        exclude group: 'commons-logging', module: 'commons-logging'
    }
}

dependencies {
    // Guava dependency
    constraints {
        implementation 'com.google.guava:guava:31.1-jre' // "-jre" for Java 8 or higher
    }
    implementation group: "com.google.guava", name:"guava"

    // Get stairway via TCL
    implementation "bio.terra:terra-common-lib" // 1.1.11-SNAPSHOT is defined in java-common-conventions

    // terra clients
    implementation "org.broadinstitute.dsde.workbench:sam-client_2.13:v0.0.213"
    implementation project(':rawls-client')

    implementation 'org.apache.commons:commons-dbcp2'
    implementation 'org.postgresql:postgresql:42.6.1'

    implementation 'com.google.auth:google-auth-library-oauth2-http'

    // https://projectlombok.org
    compileOnly 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'
    annotationProcessor group: "org.springframework.boot", name: "spring-boot-configuration-processor"

    // spring boot related dependencies
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.retry:spring-retry'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    // OpenTelemetry @WithSpan annotations:
    implementation 'io.opentelemetry.instrumentation:opentelemetry-instrumentation-annotations:2.2.0'

    // gcs
    implementation platform('com.google.cloud:libraries-bom:26.44.0')
    implementation 'com.google.cloud:google-cloud-storage'
    implementation 'com.google.cloud:google-cloud-pubsub'

    liquibaseRuntime 'info.picocli:picocli:4.6.1'
    liquibaseRuntime 'org.postgresql:postgresql:42.6.1'

    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    // prometheus metrics
    implementation 'io.micrometer:micrometer-registry-prometheus'

    // sentry
    implementation 'io.sentry:sentry:7.3.0'

    // jersey hk2 for dependency injection
    implementation 'org.glassfish.jersey.inject:jersey-hk2'

    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        // Fixes warning about multiple occurrences of JSONObject on the classpath
        exclude group: 'com.vaadin.external.google', module: 'android-json'
    }
    testImplementation 'org.mockito:mockito-inline:2.13.0'
    testImplementation 'org.apache.commons:commons-text:1.10.0'

    // dependencies for zonky embedded databases
    testImplementation 'io.zonky.test:embedded-database-spring-test:2.5.0'
    testImplementation 'org.awaitility:awaitility'
    testImplementation 'org.testcontainers:postgresql:1.17.3'

}
// workaround for local development
// set GOOGLE_APPLICATION_CREDENTIALS if this file exists - should only exist when
// write-config.sh is run.
// GOOGLE_APPLICATION_CREDENTIALS is set for us when running in a deployed environment
def googleCredentialsFile = "${rootDir}/config/teaspoons-sa.json"
bootRun {
    if(project.file(googleCredentialsFile).exists()) {
        environment.put("GOOGLE_APPLICATION_CREDENTIALS", "${googleCredentialsFile}")
    }
}

test {
    useJUnitPlatform ()
}

sonarqube {
    properties {
        property 'sonar.projectName', 'terra-scientific-pipelines-service'
        property 'sonar.projectKey', 'DataBiosphere_terra-scientific-pipelines-service'
        property 'sonar.organization', 'broad-databiosphere'
        property 'sonar.host.url', 'https://sonarcloud.io'
    }
}

jacocoTestReport {
    reports {
        // sonarqube requires XML coverage output to upload coverage data
        xml.required = true
    }
}

liquibase {
    activities {
        catalog {
            changeLogFile 'src/main/resources/db/changelog.xml'
            url 'jdbc:postgresql://localhost:5432/pipelines_db'
            username 'dbuser'
            password 'dbpwd'
            logLevel 'info'
        }
    }
}
