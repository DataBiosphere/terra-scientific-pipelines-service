plugins {
    id 'idea'
    id 'jacoco'
    id 'java'

    id 'com.diffplug.spotless'
    id 'org.hidetake.swagger.generator'
}

boolean isGithubAction = System.getenv().containsKey("GITHUB_ACTIONS")

if (!isGithubAction) {
    tasks.withType(JavaExec).configureEach {
        systemProperty 'spring.profiles.include', 'human-readable-logging'
    }
    tasks.withType(Test).configureEach {
        systemProperty 'spring.profiles.include', 'human-readable-logging'
    }
}

// removing the java toolchain gets rid of a warning about using a deprecated API:
// Note: [...]/client/build/swagger-code/src/main/java/bio/terra/pipelines/client/RFC3339DateFormat.java uses or overrides a deprecated API.

repositories {
    maven {
        // Terra proxy for maven central
        url 'https://broadinstitute.jfrog.io/broadinstitute/maven-central/'
    }
    mavenCentral()
    maven {
        url 'https://broadinstitute.jfrog.io/broadinstitute/libs-release/'
    }
    maven {
        url 'https://broadinstitute.jfrog.io/broadinstitute/libs-snapshot-local/'
    }
}

dependencies {
    compileOnly "com.google.code.findbugs:annotations:3.0.1"
    implementation 'org.slf4j:slf4j-api:2.0.12'

    testImplementation 'ch.qos.logback:logback-classic:1.5.0'
    testImplementation 'ch.qos.logback:logback-core:1.5.0' // see https://github.com/TNG/ArchUnit/issues/1212
    testImplementation 'org.hamcrest:hamcrest:2.2'

    implementation 'bio.terra:terra-common-lib:0.1.9-SNAPSHOT'

    implementation 'io.swagger.core.v3:swagger-annotations:2.2.12'

    swaggerCodegen 'io.swagger.codegen.v3:swagger-codegen-cli:3.0.47'
}

tasks.named('test') {
    useJUnitPlatform()
}

version = gradle.releaseVersion
group = 'bio.terra'

spotless {
    java {
        target 'src/*/java/**/*.java'
        googleJavaFormat()
    }
}

// Run spotless check when running in github actions, otherwise run spotless apply.
compileJava {
    if (isGithubAction) {
        dependsOn(spotlessCheck)
    } else {
        dependsOn(spotlessApply)
    }
}

